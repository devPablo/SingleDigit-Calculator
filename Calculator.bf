>>>>>>,<+++++[->---------<]>---<<<<<< (first input)
+++++++++++++++++++++++++++++++++++++++++++ (sum symbol)
>,
[
->+>+
<<
]
>>>+
<<
-------------------------------------------
[ (subtraction)
- 
>>- (cell 5)
+++++++++++++++++++++++++++++++++++++++++++++< (sub symbol)
 [
 ---------------------------------------------
  [
  (other) 
  >[-] (cell 5)
  <[-] (cell 4)
  <[-] (cell 3)
  <<[-] (cell 2)
  > (cell 1)
  ++++++++ [<++++++++++>-] <->[-]          O
  >++++++++++ [<+++++++++++>-] <++>[-]     p
  >++++++++++ [<++++++++++>-] <+>[-]       e
  >++++++++++ [<+++++++++++>-] <++++>[-]    r
  >++++++++++ [<++++++++++>-] <--->[-]     a
  >++++++++++ [<++++++++++++>-] <---->[-]  t
  >++++++++++ [<+++++++++++>-] <+>[-]      o
  >++++++++++ [<+++++++++++>-] <++++>[-]    r
  >++++++++++ [<+++++++++++>-] <+++++>[-]  s
  >++++++ [<++++++++++>-] <-->[-]          (colon)
  >+++ [<++++++++++>-] <++>[-]             (space)
  >+++++ [<+++++++++>-] <-->[-]            (sum sign)
  >+++++ [<+++++++++>-] <->[-]             (comma)
  >+++ [<++++++++++>-] <++>[-]             (space)
  >+++++ [<+++++++++>-] [-]                (sub sign)
  <<<<<<<<<<<<<<<
  .>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.
  ,
  ]
  (subtraction)
  > (cell 5)
  [-]
  << (cell 3)
  [-]
  << (cell 1)
  [-]
  +.,
 ]
] (cell 3)
< (summation) (cell 2)
>>>[-] (cell 5)
<[-] (cell 4)
<<<[-] (cell 1)

(transfer numbers)
>>>>>> (cell 6)
[
-
<<<<<<+ (cell 1)
>>>>>>
]
<<<<< (cell 2)
,>+++++[-<--------->]<---
[ (applying sum)
-<+>
]                           (0 1 0 (a) (b) 0)
< (cell 1)
>+ (cell 2)
>>+++++++++ (cell 4)
<<< (cell 1)
[
->>>>+>>+<<<<<<
]
>>> (cell 4 at position (a))
[->-[>]<<] (indicator of first zero value)

<[-
 ((a) is less than 9)
 >>[-] (cell 4)
 >>>>+++++ (cell 8)
  [
  <+++++++++>-
  ]
  <+++. (cell 7)
  , (end)
 ]

<[-
 ((a) is greater or equals 9)
 >>>[-] (cell 5)
 >+ (cell 6)
 >>++ (cell 8)
  [ (Decimal to ASCII 1st digit)
   -<----->
  ]
  +++++
  [
  -<<+++++++++>>
  ]
  <<+++ (cell 6)
  >>+++++ (cell 8)
  [ (Decimal to ASCII 2nd digit)
   -<+++++++++>
  ]
  <+++ (cell 7)
  <. (cell 6)
  >. (cell 7)
  ,  (end)
 ]